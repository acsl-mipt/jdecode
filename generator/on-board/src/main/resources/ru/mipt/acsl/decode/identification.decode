ru"Модуль динамической идентификации устройств"
namespace ru.mipt.acsl.identification

import decode.varuint

import ru.mipt.acsl.foundation.guid

language ru

"Пара номер компонента и его уникальный идентификатор"
struct component_number_guid_pair
(
  'Номер компонента в пределах устройства'
  number: varuint,
  'Уникальный идентификатор компонента'
  guid: guid,
)

"Идентификатор устройства и его корневого компонента"
struct short_id
(
  'Уникальный идентификатор устройства'
  deviceGuid: guid,
  'Уникальный идентификатор компонента устройства'
  rootComponentGuid: guid,
)

"Идентификатор устройства и всех его компонентов"
struct full_id
(
    shortId: short_id,
    'Идентификаторы всех компонентов устройства'
    deviceComponentGuidPairs: array[component_number_guid_pair, 1..*],
)

'Компонент идентификации устройства и всех его компонентов'
component Identification @id(0)
{
  parameters
  (
    fullId: full_id,
  )

  'Запросить идентификатор устройства и идентификатор компонента устройства'
  command requestShortId(): short_id

  'Запросить идентификатор устройства, компонента устройства и всех его подкомпонентов'
  command requestFullId(): full_id

  'Запросить идентификатор компонента по номеру'
  command requestComponentGuid(componentNumber: varuint): guid

  status fullId @id(0) @priority(10)
  (
    fullId
  )

  status deviceComponentNumbers
  (
    fullId.deviceComponentGuidPairs[*].number
  )
}