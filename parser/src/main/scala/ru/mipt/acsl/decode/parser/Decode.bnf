{
    parserClass = "ru.mipt.acsl.decode.parser.DecodeParser"
  parserUtilClass="ru.mipt.acsl.decode.parser.DecodeParserUtil"

    extends = "com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix = "Decode"
    psiImplClassSuffix = "Impl"
    psiPackage = "ru.mipt.acsl.decode.parser.psi"
    psiImplPackage = "ru.mipt.acsl.decode.parser.psi.impl"

    elementTypeHolderClass = "ru.mipt.acsl.decode.parser.psi.DecodeTypes"
    elementTypeClass = "ru.mipt.acsl.decode.parser.psi.DecodeElementType"
    tokenTypeClass = "ru.mipt.acsl.decode.parser.psi.DecodeTokenType"

    tokens = [
        NAMESPACE = "namespace"
        COMPONENT = "component"
        COMMAND = "command"
        //SPACE = "regexp:[\ \r\n\t\v]+"
        ESCAPED_NAME = "regexp:\^[a-zA-Z_][a-zA-Z0-9_]*"
        ELEMENT_NAME_TOKEN = "regexp:[a-zA-Z_][a-zA-Z0-9_]*"
        COMMENT = "regexp:#[^\r\n]*"
        MULTILINE_COMMENT = 'regexp:/\*([^\*][^/\*]?)*\*/'
        LEFT_BRACE = '{'
        RIGHT_BRACE = '}'
        LEFT_PAREN = '('
        RIGHT_PAREN = ')'
        LEFT_BRACKET = '['
        RIGHT_BRACKET = ']'
        COMMA = ','
        EQ_SIGN = '='
        STATUS = "status"
        MESSAGE = "message"
        EVENT = "event"
        DYNAMIC = "dynamic"
        PARAMETER = "parameter"
        STRING = 'regexp:"([^"\\]|\\.)*"'
        STRING_UNARY_QUOTES = "regexp:'([^'\\]|\\.)*'"
        SUBCOMPONENT = "subcomponent"
        PARAMETERS = "parameters"
        MEASURE_TOKEN = "measure"
        TYPE_KEYWORD = "type"
        ALIAS = "alias"
        DOTS = ".."
        DOT = "."
        SUBTYPE = "<:"
        NON_NEGATIVE_INTEGER = "regexp:[0-9]+"
        ENUM = "enum"
        PLACEMENT = "placement"
        BEFORE = "before"
        AFTER = "after"
        STRUCT = "struct"
        SLASH = '/'
        STAR = '*'
        PLUS = '+'
        MINUS = '-'
        COLON = ':'
        ARROW = '->'
        DISPLAY = "display"
        TRUE = "true"
        FALSE = "false"
        WITH = "with"
        QUESTION = "?"
        IMPORT = "import"
        AS = "as"
        LANGUAGE = "language"
        DEFAULT = "default"
        VAR = "var"
        NATIVE = "native"
        FINAL = "final"
        CONST = "const"
        EXTENDS = "extends"
        SCRIPT = "script"
        FOR = "for"
        AT = "@"
    ]
}

decode_file ::= namespace_decl import_stmt* (const_decl | component_decl | measure_decl | native_type_decl | sub_type_decl
    | enum_type_decl | struct_type_decl | alias_decl | language_decl | script_decl)*

script_decl ::= SCRIPT element_name_rule FOR component_ref LEFT_PAREN command_args? RIGHT_PAREN
    COLON type_unit_application EQ_SIGN LEFT_BRACE expr* RIGHT_BRACE

expr ::= command_call | literal | var_expr

var_expr ::= element_name_rule

command_call ::= element_id LEFT_PAREN (expr (COMMA expr)*)? RIGHT_PAREN

import_stmt ::= IMPORT element_id (DOT (LEFT_PAREN import_element (COMMA import_element)* COMMA* RIGHT_PAREN | import_element_star))?

import_element_star ::= STAR

import_element_as ::= AS element_name_rule

import_element ::= element_name_rule import_element_as?

alias_decl ::= element_info? ALIAS element_name_rule type_unit_application

namespace_decl ::= element_info? NAMESPACE element_id

element_name_rule ::= ELEMENT_NAME_TOKEN | ESCAPED_NAME

component_decl ::= element_info? COMPONENT element_name_rule annotation_decl* (WITH component_ref (COMMA component_ref)*)?
    LEFT_BRACE component_parameters_decl? (command_decl|message_decl)* RIGHT_BRACE

component_parameters_decl ::= element_info? PARAMETERS LEFT_PAREN command_args RIGHT_PAREN

command_decl ::= element_info? COMMAND element_name_rule annotation_decl* LEFT_PAREN command_args? RIGHT_PAREN
    COLON type_unit_application (EQ_SIGN LEFT_BRACE expr* RIGHT_BRACE)?

command_args ::= command_arg (COMMA command_arg)* COMMA?

command_arg ::= element_info? element_name_rule COLON type_unit_application

type_unit_application ::= type_application unit? optional?

optional ::= QUESTION

type_application ::= element_id generic_arguments?

generic_arguments ::= LEFT_BRACKET type_unit_application (COMMA type_unit_application?)* COMMA* RIGHT_BRACKET

unit ::= SLASH element_id SLASH

element_id ::= element_name_rule (DOT element_name_rule)*

message_decl ::= status_message | event_message

annotation_parameter ::= (element_name_rule EQ_SIGN)? type_unit_application

annotation_decl ::= AT element_name_rule (LEFT_PAREN annotation_parameter (COMMA annotation_parameter?)* RIGHT_PAREN)?

status_message ::= element_info? STATUS element_name_rule annotation_decl* (PRIORITY EQ_SIGN integer_literal)? status_message_parameters_decl

status_message_parameters_decl ::= LEFT_PAREN parameter_decl (COMMA parameter_decl)* COMMA? RIGHT_PAREN

event_message_parameters_decl ::= LEFT_PAREN event_parameter_decl (COMMA event_parameter_decl)* COMMA? RIGHT_PAREN

event_parameter_decl ::= element_info? (parameter_element | var_parameter_element)

var_parameter_element ::= VAR element_name_rule COLON type_unit_application

event_message ::= element_info? EVENT element_name_rule annotation_decl* type_application event_message_parameters_decl

parameter_decl ::= element_info? parameter_element

range_from_decl ::= numeric_literal

range_to_decl ::= numeric_literal

dependent_range_decl ::= STAR | range_from_decl (COMMA (range_to_decl | STAR))?

parameter_path_element ::= LEFT_BRACKET dependent_range_decl RIGHT_BRACKET | DOT element_name_rule

parameter_element ::= element_name_rule parameter_path_element*

element_info ::= string_value+

string_literal ::= STRING | STRING_UNARY_QUOTES

string_value ::= element_name_rule? string_literal

component_ref ::= element_name_rule

measure_decl ::= element_info? MEASURE_TOKEN element_name_rule (DISPLAY string_value)* (PLACEMENT (BEFORE | AFTER))?

generic_parameter ::= element_name_rule (SUBTYPE type_unit_application)?

generic_parameters ::= LEFT_BRACKET generic_parameter (COMMA generic_parameter)* COMMA* RIGHT_BRACKET

sub_type_decl ::= element_info? TYPE_KEYWORD element_name_rule generic_parameters? type_unit_application

enum_name ::= element_name_rule

enum_type_decl ::= element_info? ENUM enum_name generic_parameters?
    final_enum? (EXTENDS element_name_rule | type_unit_application)
    LEFT_PAREN enum_type_values RIGHT_PAREN

struct_type_decl ::= element_info? STRUCT element_name_rule generic_parameters?
    LEFT_PAREN command_args RIGHT_PAREN

native_type_decl ::= element_info? NATIVE element_name_rule generic_parameters?

final_enum ::= FINAL

enum_type_values ::= enum_type_value (COMMA enum_type_value)* COMMA?

enum_type_value ::= element_info? element_name_rule EQ_SIGN literal

integer_literal ::= (PLUS | MINUS)? NON_NEGATIVE_INTEGER

numeric_literal ::= float_literal | integer_literal

bool_literal ::= TRUE | FALSE

literal ::= numeric_literal | bool_literal

const_decl ::= element_info? CONST element_name_rule literal

float_literal ::= (PLUS | MINUS)? NON_NEGATIVE_INTEGER DOT NON_NEGATIVE_INTEGER (("e" | "E") (PLUS | MINUS)? NON_NEGATIVE_INTEGER)?

language_decl ::= LANGUAGE element_name_rule