{
    parserClass = "ru.mipt.acsl.decode.parser.idea.DecodeParser"
  parserUtilClass="ru.mipt.acsl.decode.parser.idea.DecodeParserUtil"

    extends = "com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix = "Decode"
    psiImplClassSuffix = "Impl"
    psiPackage = "ru.mipt.acsl.decode.parser.psi"
    psiImplPackage = "ru.mipt.acsl.decode.parser.psi.impl"

    elementTypeHolderClass = "ru.mipt.acsl.decode.parser.psi.DecodeTypes"
    elementTypeClass = "ru.mipt.acsl.decode.parser.psi.DecodeElementType"
    tokenTypeClass = "ru.mipt.acsl.decode.parser.psi.DecodeTokenType"

    tokens = [
        NAMESPACE = "namespace"
        COMPONENT = "component"
        COMMAND = "command"
        ESCAPED_NAME = "regexp:\^[a-zA-Z_][a-zA-Z0-9_]*"
        ELEMENT_NAME_TOKEN = "regexp:[a-zA-Z_][a-zA-Z0-9_]*"
        COMMENT = "regexp:#[^\r\n]*"
        MULTILINE_COMMENT = 'regexp:/\*([^\*][^/\*]?)*\*/'
        LEFT_BRACE = '{'
        RIGHT_BRACE = '}'
        LEFT_PAREN = '('
        RIGHT_PAREN = ')'
        LEFT_BRACKET = '['
        RIGHT_BRACKET = ']'
        COMMA = ','
        EQ_SIGN = '='
        STATUS = "status"
        MESSAGE = "message"
        EVENT = "event"
        DYNAMIC = "dynamic"
        PARAMETER = "parameter"
        INFO = "info"
        STRING = 'regexp:"([^"\\]|\\.)*"'
        STRING_UNARY_QUOTES = "regexp:'([^'\\]|\\.)*'"
        SUBCOMPONENT = "subcomponent"
        PARAMETERS = "parameters"
        UNIT_TOKEN = "unit"
        TYPE_KEYWORD = "type"
        ALIAS = "alias"
        DOTS = ".."
        DOT = "."
        NON_NEGATIVE_NUMBER = "regexp:[0-9]+"
        UINT = "uint"
        INT = "int"
        FLOAT = "float"
        BOOL = "bool"
        ARRAY = "array"
        BER = "ber"
        ENUM = "enum"
        PLACEMENT = "placement"
        BEFORE = "before"
        AFTER = "after"
        BASE_TYPE = "base_type"
        STRUCT = "struct"
        SLASH = '/'
        STAR = '*'
        PLUS = '+'
        MINUS = '-'
        COLON = ':'
        ARROW = '->'
        LT = '<'
        GT = '>'
        DISPLAY = "display"
        TRUE = "true"
        FALSE = "false"
        WITH = "with"
        QUESTION = "?"
        IMPORT = "import"
        AS = "as"
        ID = "id"
        LANGUAGE = "language"
        DEFAULT = "default"
        PRIORITY = "priority"
        VAR = "var"
    ]
}

decodeFile ::= namespace_decl import_stmt* (component_decl | unit_decl | type_decl | alias_decl | language_decl)*

import_stmt ::= IMPORT element_id (DOT import_element)* (DOT (LEFT_BRACE import_element (COMMA import_element)* COMMA? RIGHT_BRACE | STAR))?

import_element ::= element_name_rule (AS element_name_rule)?

alias_decl ::= info_string? ALIAS element_id type_application

namespace_decl ::= info_string? NAMESPACE element_id

element_name_rule ::= ELEMENT_NAME_TOKEN | ESCAPED_NAME

component_decl ::= info_string? COMPONENT element_name_rule entity_id? (WITH subcomponent_decl (COMMA subcomponent_decl)*)?
    LEFT_BRACE component_parameters_decl? (command_decl|message_decl)* RIGHT_BRACE

component_parameters_decl ::= PARAMETERS LEFT_PAREN command_args RIGHT_PAREN

command_decl ::= info_string? COMMAND element_name_rule entity_id? LEFT_PAREN command_args? RIGHT_PAREN (ARROW type_unit_application)?

command_args ::= command_arg (COMMA command_arg)* COMMA?

command_arg ::= info_string? type_unit_application element_name_rule

type_unit_application ::= type_application unit?

type_application ::= (generic_type_application | primitive_type_application | array_type_application) QUESTION?

generic_type_application ::= element_id (LT type_unit_application (COMMA type_unit_application)* COMMA? GT)?

unit ::= SLASH element_id SLASH

primitive_type_application ::= primitive_type_kind COLON NON_NEGATIVE_NUMBER | native_type_kind

array_type_application ::= LEFT_BRACKET type_application (COMMA length_from (DOTS (length_to | STAR))?)? RIGHT_BRACKET

struct_type_decl ::= info_string? STRUCT LEFT_PAREN command_args RIGHT_PAREN

element_id ::= element_name_rule (DOT element_name_rule)*

message_decl ::= status_message | event_message

entity_id ::= ID COLON NON_NEGATIVE_NUMBER

status_message ::= info_string? STATUS element_name_rule entity_id? (PRIORITY COLON NON_NEGATIVE_NUMBER)? status_message_parameters_decl

status_message_parameters_decl ::= LEFT_PAREN parameter_decl (COMMA parameter_decl)* COMMA? RIGHT_PAREN

event_message_parameters_decl ::= LEFT_PAREN event_parameter_decl (COMMA event_parameter_decl)* COMMA? RIGHT_PAREN

event_parameter_decl ::= info_string? (parameter_element | var_parameter_element)

var_parameter_element ::= VAR type_unit_application element_name_rule

//deep_all_parameters ::= STAR DOT STAR

//all_parameters ::= STAR

event_message ::= info_string? EVENT element_name_rule entity_id? type_application event_message_parameters_decl

//dynamic_status_message ::= DYNAMIC STATUS message_parameters_decl

parameter_decl ::= info_string? parameter_element

parameter_element ::= element_id ((LEFT_BRACKET NON_NEGATIVE_NUMBER (DOTS NON_NEGATIVE_NUMBER)? RIGHT_BRACKET)+
    (DOT element_id (LEFT_BRACKET NON_NEGATIVE_NUMBER (DOTS NON_NEGATIVE_NUMBER)? RIGHT_BRACKET)?)*)?

info_string ::= string_value

string_value ::= STRING | STRING_UNARY_QUOTES

subcomponent_decl ::= element_name_rule

unit_decl ::= info_string? UNIT_TOKEN element_name_rule (DISPLAY string_value)? (PLACEMENT (BEFORE | AFTER))?

type_decl ::= info_string? TYPE_KEYWORD element_name_rule type_decl_body

primitive_type_kind ::= UINT | INT | FLOAT | BOOL

native_type_kind ::= BER

length_from ::= NON_NEGATIVE_NUMBER

length_to ::= NON_NEGATIVE_NUMBER

type_decl_body ::= (info_string? type_application) | enum_type_decl | struct_type_decl

enum_type_decl ::= info_string? ENUM type_application LEFT_PAREN enum_type_values RIGHT_PAREN

enum_type_values ::= enum_type_value (COMMA enum_type_value)* COMMA?

enum_type_value ::= info_string? element_name_rule EQ_SIGN literal

literal ::= float_literal | NON_NEGATIVE_NUMBER | TRUE | FALSE

float_literal ::= (PLUS | MINUS)? (NON_NEGATIVE_NUMBER DOT NON_NEGATIVE_NUMBER? | DOT NON_NEGATIVE_NUMBER) (("e" | "E") (PLUS | MINUS)? NON_NEGATIVE_NUMBER)?

language_decl ::= LANGUAGE element_name_rule DEFAULT?